#compdef pkgcheck

SHELL_LIB=$(python -c 'import pkgcore._const as const; print(const.LIBDIR_PATH)')/shell/zsh/pkgcore.zsh
source "${SHELL_LIB}" || print "$0: failed to load '${SHELL_LIB}'" >&2
unset SHELL_LIB

typeset -a common_main_args common_output_args common_args

common_main_args=(
  '(- :)'--version'[show version information and exit]'
  '--add-config[modify an existing configuration section]'
  '--new-config[add a new configuration section]'
  '--empty-config[do not load user/system configuration]'
  '--config[override location of config files]:config path:_files'
  "--color[Color output]:yes/no:((y\:'yes' n\:'no'))"
)

common_output_args=(
  '(- :)'{-h,--help}'[show help information and exit]'
  '(--debug --help -h)--debug[enable debugging output]'
  '(--quiet -q --verbose -v)'{-q,--quiet}'[suppress non-error output]'
  '(--verbose -v --quiet -q)'{-v,--verbose}'[show verbose output]'
)

common_args=(
  $common_main_args
  $common_output_args
)

local curcontext=$curcontext state state_descr line ret=1
typeset -a options arches profiles plugin_options

options=(
  "--reporter[use a non-default reporter (defined in pkgcore's config]"
  '--list-keywords[print available warning/error keywords and exit]'
  '--list-checks[print what checks are available to run and exit]'
  '--list-reporters[print known reporters and exit]'
  {'(--repo)-r','(-r)--repo'}'[repo to pull packages from]:repo:_repos'
  {'(--suite)-s','(-s)--suite'}'[specify the configuration suite to use]'
)

check_options=(
  {'(--checks)-c','(-c)--checks'}'[comma separated list of checks to enable/disable]'
  {'(--checkset)-C','(-C)--checkset'}'[preconfigured set of checks to run]'
)

arches=(
  {'(--arches)-a','(-a)--arches'}'[comma separated list of arches to enable/disable]:arches:{_values -s , arches $(_arches -p)}'
)

profiles=(
  '--profiles-base[path to base profiles directory]:profiles base path:_path_files -/'
  '--profiles-disable-deprecated[disable scanning of deprecated profiles]'
  {'(--profiles)-p','(-p)--profiles'}'[comma separated list of profiles to enable/disable]:arches:{_values -s , profiles stable dev exp}'
)

plugin_options=(
  $arches
  $profiles
)

_arguments -C \
  $common_args \
  $options \
  $check_options \
  $plugin_options \
  && ret=0

return ret

# vim: set et sw=2 ts=2 ft=zsh:
